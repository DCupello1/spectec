;;
;; Projections
;;

def $funcs(externval*) : funcaddr*
def $funcs(eps) = eps
def $funcs((FUNC fa) externval'*) = fa $funcs(externval'*)
def $funcs(externval externval'*) = $funcs(externval'*)
  -- otherwise

def $globals(externval*) : globaladdr*
def $globals(eps) = eps
def $globals((GLOBAL ga) externval'*) = ga $globals(externval'*)
def $globals(externval externval'*) = $globals(externval'*)
  -- otherwise

def $tables(externval*) : tableaddr*
def $tables(eps) = eps
def $tables((TABLE ta) externval'*) = ta $tables(externval'*)
def $tables(externval externval'*) = $tables(externval'*)
  -- otherwise

def $mems(externval*) : memaddr*
def $mems(eps) = eps
def $mems((MEM ma) externval'*) = ma $mems(externval'*)
def $mems(externval externval'*) = $mems(externval'*)
  -- otherwise


;;
;; Allocation
;;

;; Definitions

def $allocfunc(store, moduleinst, func) : (store, funcaddr)
def $allocfunc(s, mm, func) = (s[.FUNCS =.. fi], |s.FUNCS|)
  -- if fi = { TYPE mm.TYPES[x], MODULE mm, CODE func }
  -- if func = FUNC x local* expr

def $allocfuncs(store, moduleinst, func*) : (store, funcaddr*)
def $allocfuncs(s, mm, eps) = (s, eps)
def $allocfuncs(s, mm, func func'*) = (s_2, fa fa'*)
  -- if (s_1, fa) = $allocfunc(s, mm, func)
  -- if (s_2, fa'*) = $allocfuncs(s_1, mm, func'*)

def $allocglobal(store, globaltype, val) : (store, globaladdr)
def $allocglobal(s, globaltype, val) = (s[.GLOBALS =.. gi], |s.GLOBALS|)
  -- if gi = { TYPE globaltype, VALUE val }

def $allocglobals(store, globaltype*, val*) : (store, globaladdr*)
def $allocglobals(s, eps, eps) = (s, eps)
def $allocglobals(s, globaltype globaltype'*, val val'*) = (s_2, ga ga'*)
  -- if (s_1, ga) = $allocglobal(s, globaltype, val)
  -- if (s_2, ga'*) = $allocglobals(s_1, globaltype'*, val'*)

def $alloctable(store, tabletype) : (store, tableaddr)
def $alloctable(s, `[i .. j]) = (s[.TABLES =.. ti], |s.TABLES|)
  -- if ti = { TYPE `[i .. j], REFS eps^i }

def $alloctables(store, tabletype*) : (store, tableaddr*)
def $alloctables(s, eps) = (s, eps)
def $alloctables(s, tabletype tabletype'*) = (s_2, ta ta'*)
  -- if (s_1, ta) = $alloctable(s, tabletype)
  -- if (s_2, ta'*) = $alloctables(s_1, tabletype'*)

def $allocmem(store, memtype) : (store, memaddr)
def $allocmem(s, `[i .. j]) = (s[.MEMS =.. mi], |s.MEMS|)
  -- if mi = { TYPE `[i .. j], BYTES 0^(i * $($(64 * $Ki))) }

def $allocmems(store, memtype*) : (store, memaddr*)
def $allocmems(s, eps) = (s, eps)
def $allocmems(s, memtype memtype'*) = (s_2, ma ma'*)
  -- if (s_1, ma) = $allocmem(s, memtype)
  -- if (s_2, ma'*) = $allocmems(s_1, memtype'*)


;; Modules

def $instexport(funcaddr*, globaladdr*, tableaddr*, memaddr*, export) : exportinst
def $instexport(fa*, ga*, ta*, ma*, EXPORT name (FUNC x)) = { NAME name, VALUE (FUNC fa*[x]) }
def $instexport(fa*, ga*, ta*, ma*, EXPORT name (GLOBAL x)) = { NAME name, VALUE (GLOBAL ga*[x]) }
def $instexport(fa*, ga*, ta*, ma*, EXPORT name (TABLE x)) = { NAME name, VALUE (TABLE ta*[x]) }
def $instexport(fa*, ga*, ta*, ma*, EXPORT name (MEM x)) = { NAME name, VALUE (MEM ma*[x]) }


def $allocmodule(store, module, externval*, val*) : (store, moduleinst)
def $allocmodule(s, module, externval*, val*) = (s_4, mm)
  -- if module =
    MODULE
      (TYPE ft)*
      import*
      func^n_func
      (GLOBAL globaltype expr_1)^n_global
      (TABLE tabletype)^n_table
      (MEMORY memtype)^n_mem
      elem*
      data*
      start?
      export*
  -- if fa_ex* = $funcs(externval*)
  -- if ga_ex* = $globals(externval*)
  -- if ta_ex* = $tables(externval*)
  -- if ma_ex* = $mems(externval*)
  -- if fa* = $(|s.FUNCS|+i_func)^(i_func<n_func)
  -- if ga* = $(|s.GLOBALS|+i_global)^(i_global<n_global)
  -- if ta* = $(|s.TABLES|+i_table)^(i_table<n_table)
  -- if ma* = $(|s.MEMS|+i_mem)^(i_mem<n_mem)
  -- if xi* = $instexport(fa_ex* fa*, ga_ex* ga*, ta_ex* ta*, ma_ex* ma*, export)*
  -- if mm = {
      TYPES ft*,
      FUNCS fa_ex* fa*,
      GLOBALS ga_ex* ga*,
      TABLES ta_ex* ta*,
      MEMS ma_ex* ma*,
      EXPORTS xi*
    }
  -- if (s_1, fa*) = $allocfuncs(s, mm, func^n_func)
  -- if (s_2, ga*) = $allocglobals(s_1, globaltype^n_global, val*)
  -- if (s_3, ta*) = $alloctables(s_2, tabletype^n_table)
  -- if (s_4, ma*) = $allocmems(s_3, memtype^n_mem)


;;
;; Instantiation
;;

def $initelem(store, moduleinst, u32*, (funcaddr*)*) : store
def $initelem(s, mm, eps, eps) = s
def $initelem(s, mm, i i'*, (a*) (a'*)*) = s_2
  -- if s_1 = s[.TABLES[mm.TABLES[0]].REFS[i:|a*|] = a*]
  -- if s_2 = $initelem(s_1, mm, i'*, (a'*)*)

def $initdata(store, moduleinst, u32*, (byte*)*) : store
def $initdata(s, mm, eps, eps) = s
def $initdata(s, mm, i i'*, (b*) (b'*)*) = s_2
  -- if s_1 = s[.MEMS[mm.MEMS[0]].BYTES[i:|b*|] = b*]
  -- if s_2 = $initdata(s_1, mm, i'*, (b'*)*)

def $instantiate(store, module, externval*) : config
def $instantiate(s, module, externval*) = s_3; f; (CALL x')?
  -- if module = MODULE type* import* func* global* table* mem* elem* data* start? export*
  -- if type* = (TYPE functype)*
  -- if global* = (GLOBAL globaltype expr_G)*
  -- if elem* = (ELEM expr_E x*)*
  -- if data* = (DATA expr_D b*)*
  -- if start? = (START x')?
  -- if n_F = |func*|
  -- if mm_init = {
      TYPES functype*,
      FUNCS $funcs(externval*) $(|s.FUNCS|+i_F)^(i_F<n_F),
      GLOBALS $globals(externval*)
    }
  -- if f_init = { MODULE mm_init }
  -- if z = s; f_init
  -- (Eval_expr : z; expr_G ~>* z; val)*
  -- (Eval_expr : z; expr_E ~>* z; (CONST I32 i_E))*
  -- (Eval_expr : z; expr_D ~>* z; (CONST I32 i_D))*
  -- if (s_1, mm) = $allocmodule(s, module, externval*, val*)
  -- if s_2 = $initelem(s_1, mm, i_E*, mm.FUNCS[x]**)
  -- if s_3 = $initdata(s_2, mm, i_D*, b**)
  -- if f = { MODULE mm }


;;
;; Invocation
;;

def $invoke(store, funcaddr, val*) : config
def $invoke(s, fa, val^n) = s; f; val^n (CALL_ADDR fa)
  -- if f = { MODULE {} }
  -- if $funcinst((s; f))[fa].TYPE = t_1^n -> t_2*
