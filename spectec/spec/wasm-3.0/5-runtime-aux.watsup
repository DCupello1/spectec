;;
;; Auxiliary definitions on Types
;;

;; Instantiation

def $inst_valtype(moduleinst, valtype) : valtype  hint(show $inst_(%,%)) hint(macro "insttype")
def $inst_reftype(moduleinst, reftype) : reftype  hint(show $inst_(%,%)) hint(macro "insttype")

;; TODO: make inlining moduleinst.TYPES work
def $inst_valtype(moduleinst, t) = $subst_all_valtype(t, dt*)
  -- if dt* = moduleinst.TYPES
def $inst_reftype(moduleinst, rt) = $subst_all_reftype(rt, dt*)
  -- if dt* = moduleinst.TYPES


;;
;; Auxiliary definitions on Values
;;

;; Default values

def $default_(valtype) : val?

def $default_(Inn) = (CONST Inn 0)
def $default_(Fnn) = (CONST Fnn $fzero($size(Fnn)))
def $default_(Vnn) = (VCONST Vnn 0)
def $default_(REF NULL ht) = (REF.NULL ht)
def $default_(REF ht) = eps


;; Packed fields

def $packfield(storagetype, val) : fieldval         hint(show $pack_(%,%)) hint(macro "packfield")
def $unpackfield(storagetype, sx?, fieldval) : val  hint(show $unpack_(%)^(%)#((%))) hint(macro "unpackfield")

def $packfield(valtype, val) = val
def $packfield(packtype, CONST I32 i) = PACK packtype $wrap(32, $psize(packtype), i)

def $unpackfield(valtype, eps, val) = val
def $unpackfield(packtype, sx, PACK packtype i) = CONST I32 $ext($psize(packtype), 32, sx, i)


;; Projections

def $funcsxv(externval*) : funcaddr*     hint(show $funcs(%))   hint(macro "funcsxv")
def $globalsxv(externval*) : globaladdr* hint(show $globals(%)) hint(macro "globalsxv")
def $tablesxv(externval*) : tableaddr*   hint(show $tables(%))  hint(macro "tablesxv")
def $memsxv(externval*) : memaddr*       hint(show $mems(%))    hint(macro "memsxv")

def $funcsxv(eps) = eps
def $funcsxv((FUNC fa) xv*) = fa $funcsxv(xv*)
def $funcsxv(externval xv*) = $funcsxv(xv*)  -- otherwise

def $globalsxv(eps) = eps
def $globalsxv((GLOBAL ga) xv*) = ga $globalsxv(xv*)
def $globalsxv(externval xv*) = $globalsxv(xv*)  -- otherwise

def $tablesxv(eps) = eps
def $tablesxv((TABLE ta) xv*) = ta $tablesxv(xv*)
def $tablesxv(externval xv*) = $tablesxv(xv*)  -- otherwise

def $memsxv(eps) = eps
def $memsxv((MEM ma) xv*) = ma $memsxv(xv*)
def $memsxv(externval xv*) = $memsxv(xv*)  -- otherwise


;;
;; Auxiliary definitions on State
;;

;; Short-hands

def $store(state) : store             hint(show %.STORE) hint(macro "Z%")
def $frame(state) : frame             hint(show %.FRAME) hint(macro "Z%")

def $store((s; f)) = s
def $frame((s; f)) = f

def $moduleinst(state) : moduleinst   hint(show %.MODULE)  hint(macro "Z%")
def $funcinst(state) : funcinst*      hint(show %.FUNCS)   hint(macro "Z%")
def $globalinst(state) : globalinst*  hint(show %.GLOBALS) hint(macro "Z%")
def $tableinst(state) : tableinst*    hint(show %.TABLES)  hint(macro "Z%")
def $meminst(state) : meminst*        hint(show %.MEMS)    hint(macro "Z%")
def $eleminst(state) : eleminst*      hint(show %.ELEMS)   hint(macro "Z%")
def $datainst(state) : datainst*      hint(show %.DATAS)   hint(macro "Z%")
def $structinst(state) : structinst*  hint(show %.STRUCTS) hint(macro "Z%")
def $arrayinst(state) : arrayinst*    hint(show %.ARRAYS)  hint(macro "Z%")

def $moduleinst((s; f)) = f.MODULE
def $funcinst((s; f)) = s.FUNCS
def $globalinst((s; f)) = s.GLOBALS
def $tableinst((s; f)) = s.TABLES
def $meminst((s; f)) = s.MEMS
def $eleminst((s; f)) = s.ELEMS
def $datainst((s; f)) = s.DATAS
def $structinst((s; f)) = s.STRUCTS
def $arrayinst((s; f)) = s.ARRAYS

def $type(state, typeidx) : deftype         hint(show %.TYPES[%])   hint(macro "Z%")
def $func(state, funcidx) : funcinst        hint(show %.FUNCS[%])   hint(macro "Z%")
def $global(state, globalidx) : globalinst  hint(show %.GLOBALS[%]) hint(macro "Z%")
def $table(state, tableidx) : tableinst     hint(show %.TABLES[%])  hint(macro "Z%")
def $mem(state, memidx) : meminst           hint(show %.MEMS[%])    hint(macro "Z%")
def $elem(state, tableidx) : eleminst       hint(show %.ELEMS[%])   hint(macro "Z%")
def $data(state, dataidx) : datainst        hint(show %.DATAS[%])   hint(macro "Z%")
def $local(state, localidx) : val?          hint(show %.LOCALS[%])  hint(macro "Z%")

def $type((s; f), x) = f.MODULE.TYPES[x]
def $func((s; f), x) = s.FUNCS[f.MODULE.FUNCS[x]]
def $global((s; f), x) = s.GLOBALS[f.MODULE.GLOBALS[x]]
def $table((s; f), x) = s.TABLES[f.MODULE.TABLES[x]]
def $mem((s; f), x) = s.MEMS[f.MODULE.MEMS[x]]
def $elem((s; f), x) = s.ELEMS[f.MODULE.ELEMS[x]]
def $data((s; f), x) = s.DATAS[f.MODULE.DATAS[x]]
def $local((s; f), x) = f.LOCALS[x]


;; Update

def $with_local(state, localidx, val) : state               hint(show %[.LOCALS[%] = %])            hint(macro "Z%")
def $with_global(state, globalidx, val) : state             hint(show %[.GLOBALS[%].VALUE = %])     hint(macro "ZG%")
def $with_table(state, tableidx, nat, ref) : state          hint(show %[.TABLES[%].REFS[%] = %])    hint(macro "ZT%")
def $with_tableinst(state, tableidx, tableinst) : state     hint(show %[.TABLES[%] = %])            hint(macro "Z%")
def $with_mem(state, memidx, nat, nat, byte*) : state       hint(show %[.MEMS[%].BYTES[% : %] = %]) hint(macro "ZM%")
def $with_meminst(state, memidx, meminst) : state           hint(show %[.MEMS[%] = %])              hint(macro "Z%")
def $with_elem(state, elemidx, ref*) : state                hint(show %[.ELEMS[%].REFS = %])        hint(macro "ZE%")
def $with_data(state, dataidx, byte*) : state               hint(show %[.DATAS[%].BYTES = %])       hint(macro "ZD%")
def $with_struct(state, structaddr, nat, fieldval) : state  hint(show %[.STRUCTS[%].FIELDS[%] = %]) hint(macro "ZS%")
def $with_array(state, arrayaddr, nat, fieldval) : state    hint(show %[.ARRAYS[%].FIELDS[%] = %])  hint(macro "ZA%")

def $with_local((s; f), x, v) = s; f[.LOCALS[x] = v]
def $with_global((s; f), x, v) = s[.GLOBALS[f.MODULE.GLOBALS[x]].VALUE = v]; f
def $with_table((s; f), x, i, r) = s[.TABLES[f.MODULE.TABLES[x]].REFS[i] = r]; f
def $with_tableinst((s; f), x, ti) = s[.TABLES[f.MODULE.TABLES[x]] = ti]; f
def $with_mem((s; f), x, i, j, b*) = s[.MEMS[f.MODULE.MEMS[x]].BYTES[i : j] = b*]; f
def $with_meminst((s; f), x, mi) = s[.MEMS[f.MODULE.MEMS[x]] = mi]; f
def $with_elem((s; f), x, r*) = s[.ELEMS[f.MODULE.ELEMS[x]].REFS = r*]; f
def $with_data((s; f), x, b*) = s[.DATAS[f.MODULE.DATAS[x]].BYTES = b*]; f
def $with_struct((s; f), a, i, fv) = s[.STRUCTS[a].FIELDS[i] = fv]; f
def $with_array((s; f), a, i, fv) = s[.ARRAYS[a].FIELDS[i] = fv]; f

def $ext_structinst(state, structinst*) : state  hint(show %[.STRUCTS =.. %]) hint(macro "Z%")
def $ext_arrayinst(state, arrayinst*) : state    hint(show %[.ARRAYS =.. %])  hint(macro "Z%")

def $ext_structinst((s; f), si*) = s[.STRUCTS =.. si*]; f
def $ext_arrayinst((s; f), ai*) = s[.ARRAYS =.. ai*]; f


;; Growing

def $growtable(tableinst, nat, ref) : tableinst hint(partial)
def $growmem(meminst, nat) : meminst            hint(partial)

def $growtable(tableinst, n, r) = tableinst'
  -- if tableinst = { TYPE (`[i .. j] rt), REFS r'* }
  -- if tableinst' = { TYPE (`[i' .. j] rt), REFS r'* r^n }
  -- if $(i' = |r'*| + n <= j)

def $growmem(meminst, n) = meminst'
  -- if meminst = { TYPE (`[i .. j] PAGE), BYTES b* }
  -- if meminst' = { TYPE (`[i' .. j] PAGE), BYTES b* (0x00)^(n * $($(64 * $Ki))) }
  -- if $(i' = |b*| / (64 * $Ki) + n <= j)
